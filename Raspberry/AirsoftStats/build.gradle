plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.12.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1'
    implementation "org.json:json:20250517"
}


java {
    sourceCompatibility = JavaVersion.VERSION_24
    targetCompatibility = JavaVersion.VERSION_24
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'me.madmagic.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    setDestinationDirectory(file(project.rootDir))
}

remotes {
    airsoftpi {
        host = 'airsoftpi.local'   // will match your ~/.ssh/config entry
        user = 'pi'    // technically redundant if ssh config sets User
        identity = file(System.getProperty("user.home") + "/.ssh/id_ed25519")
    }
}

tasks.register('FTPUpload') {
    dependsOn tasks.named('jar')

    doLast {
        def jarFile = tasks.named('jar').get().archiveFile.get().asFile
        ssh.run {
            session(remotes.airsoftpi) {
                put from: jarFile, into: '/home/pi/'
            }
        }
    }
}